buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }

        maven {
            url 'https://mvnrepository.com/artifact/org.json/json'
        }

        maven {
            url 'https://mvnrepository.com/artifact/com.codesnippets4all/quick-json'
        }

        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "4.0.4"
    id 'idea'
}
apply plugin: 'net.minecraftforge.gradle.forge'



sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

def buildnumber = System.getenv("BUILD_NUMBER")
def travisbuildnumber = (System.getenv("TRAVIS_BUILD_NUMBER") ?: -000).toInteger() + 00
version = "${minecraft_version}-${mod_version}.${buildnumber ?: travisbuildnumber}"
group = group_id // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = mod_id

def numina_version = (System.getenv("NUMINA_VERSION") ?: "${version}")

repositories {
    //url                                                           //dependencies
    maven { url "http://dvs1.progwml6.com/files/maven" }            //JEI
    maven { url = "http://maven.covers1624.net/" }                  //CoFH
    maven { url = "http://maven.ic2.player.to" }                    //IC2 Forestry
    maven { url = "http://maven.tterrag.com" }                      //EnderIO
    maven { url = "http://maven2.tterrag.com" }                     //EnderIO
    maven { url = 'http://maven.mcmoddev.com' }                     //TESLA API
    maven { url = 'https://mod-buildcraft.com/maven/' }             //BuildCraft
    maven { url = 'http://maven.cil.li/' }                          //Scannable
    maven { url = 'https://repo.raoulvdberge.com/' }                //RefinedStorage
    maven { url = 'http://maven.thiakil.com/' }                     //Mekanism
    maven { url = "http://maven.blamejared.com" }                   //CraftTweaker - required by Mekanism
    maven { url = 'https://minecraft.curseforge.com/api/maven/' }   //Thaumcraft
    mavenCentral()
}

dependencies {
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.6'
    shadow group: 'org.apache.commons', name: 'commons-text', version: '1.6'
    deobfCompile "mezz.jei:jei_${minecraft_version}:${jei_version}:api"                             //JEI API
    deobfCompile "cofh:CoFHCore:${minecraft_version}-${cofhcore_version}:universal"                 //CoFH Core
    deobfCompile "appeng:appliedenergistics2:${ae2api_version}"                                     //Applied Energetics 2
    deobfCompile "net.industrial-craft:industrialcraft-2:${ic2api_version}-ex112"                   //IC2 API
    compileOnly "com.enderio.core:EnderCore:${minecraft_version}-${endercore_version}"              //Ender Core
    deobfCompile "net.sengir.forestry:forestry_${minecraft_version}:${forestry_version}"            //Forestry
    deobfCompile "thaumcraft:Thaumcraft:1.12.2:${thaumcraft_version}"                               //Thaumcraft
    deobfCompile "refinedstorage:refinedstorage:${refinedstorage_version}"                          //Refined Storage
    compile "net.darkhax.tesla:Tesla-${minecraft_version}:${tesla_version}"                         //Tesla
    compile "com.mod-buildcraft:buildcraft:${buildcraft_version}"                                   //BuildCraft
    compile "li.cil.scannable:Scannable:MC${minecraft_version}-${scannable_version}"                //Scannable
    compile fileTree(dir: '/libs', include: 'RenderPlayerAPI-1.12.1-1.0.jar')                    //RenderPlayerAPI
    compile fileTree(dir: '/libs', include: 'ExtraCells-1.12.2-2.6.1a.jar')                      //ExtraCells
    compile("mekanism:Mekanism:${minecraft_version}-${mekanism_version}")                           //Mekanism
            { exclude group: 'mezz.jei' }//Mek has implemented a duplicated and out date JEI
    compileOnly ("com.enderio:EnderIO:${minecraft_version}-${enderio_version}:api")                 //EnderIO API
            { exclude group: "deobf.com.enderio.core"}//Another duplicated and (dirty words) dependency
    
    //in fact,if you want run with EnderIO,you had to manually add EnderCore and EnderIO to your run/mods
    //or you can help to add some lines below that can help the "runClient" works properly while also load EnderIO
    
    compile group: 'com.google.guava', name: 'guava', version: '22.0'

    //we need baubles at runtime
    runtime "com.azanor:Baubles:${baubles_runtime_version}"
    
    // https://mvnrepository.com/artifact/org.json/json
    compile group: 'org.json', name: 'json', version: '20180813' // probably get rid of this
    compile group: 'com.codesnippets4all', name: 'quick-json', version: '1.0.4'
}

shadowJar {
    archiveName = tasks.jar.archiveName
    classifier = ""
    exclude 'org.apache.commons.lang*'
    relocate 'org.apache.commons.text', "net.machinemuse.repack.org.apache.commons.text"
    configurations = [project.configurations.shadow]
}

reobf {
    shadowJar {
                mappingType = "SEARGE"
            }
}

tasks.reobfShadowJar.mustRunAfter shadowJar


minecraft {
    version = minecraft_version + "-" + forge_version// + "-" + "1.10.0"
    runDir = "run"
    mappings = mcp_mappings
    replace "@numina_version@", numina_version
    replace "@VERSION@", project.version
    useDepAts = true

}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    // Move access transformers to META-INF
    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    manifest {
        attributes 'FMLAT': 'numina_at.cfg'
    }
}

//task devJar(type: Jar) {
//    from sourceSets.main.output
//    classifier = 'dev'
//}
//
//artifacts {
//    archives devJar
//}

idea {
    module {
        inheritOutputDirs = true
    }
}

runClient {
    jvmArgs "-Xmx3G"
}